Sub makePowerPoint()
' creates a PowerPoint containing all charts, with sections by worksheet
    
    ' initialize variables '
    Dim PPApp As PowerPoint.Application
    Dim PPPresentation As PowerPoint.Presentation
    Dim PPSlide  As PowerPoint.Slide
    Dim PPCharts As Excel.ChartObject
    Dim ws As Worksheet
    Dim wbname As String
    Dim wb As Workbook
    Dim sectionIndex As Integer
    
    ' define variables '
    Set wb = ActiveWorkbook
    Set ws = wb.Sheets(1)
    sectionIndex = 1
    
    ' create presentation '
    Set PPApp = New PowerPoint.Application
    PPApp.Visible = True
    PPApp.WindowState = ppWindowMaximized
    Set PPPresentation = PPApp.Presentations.Add
    
    ' ==== TITLE SLIDE ==== '
    
    Set PPSlide = PPPresentation.Slides.Add(1, ppLayoutTitle)
    With PPSlide.Shapes(1).TextFrame.TextRange
        .Text = "Charts From " & wb.Name
        .Font.Size = 60
        .Font.Color.RGB = RGB(0, 120, 20)
        .Font.Name = "Luxi Sans"
    End With
    With PPSlide.Shapes(2).TextFrame.TextRange
        .Text = "Automatically copied from Excel file"
        .Font.Bold = msoTrue
        .Font.Name = "Microsoft YaHei Light"
        .Font.Color.RGB = RGB(120, 120, 120)
    End With
    
    ' iterate through each worksheet in the workbook '
    For Each ws In wb.Worksheets
    
        If ws.ChartObjects.Count = 0 Then GoTo nextSheet 'skip sheets that have no charts
        
        ' ==== SECTION HEADERS ==== '
    
        PPApp.ActivePresentation.Slides.Add PPApp.ActivePresentation.Slides.Count + 1, ppLayoutSectionHeader
        PPApp.ActiveWindow.View.GotoSlide PPApp.ActivePresentation.Slides.Count
        Set PPSlide = PPApp.ActivePresentation.Slides(PPApp.ActivePresentation.Slides.Count)
        PPSlide.Shapes(1).TextFrame.TextRange.Text = "Section " & sectionIndex & ": " & ws.Name
        sectionIndex = sectionIndex + 1
        
        ' iterate through each chart in the worksheet '
        For Each PPCharts In ws.ChartObjects
            
            ' ==== CHART SLIDES ==== '
    
            PPApp.ActivePresentation.Slides.Add PPApp.ActivePresentation.Slides.Count + 1, ppLayoutTextAndChart
            PPApp.ActiveWindow.View.GotoSlide PPApp.ActivePresentation.Slides.Count
            Set PPSlide = PPApp.ActivePresentation.Slides(PPApp.ActivePresentation.Slides.Count)
            
            ' add in chart '
            PPCharts.Copy
            PPSlide.Shapes(1).TextFrame.TextRange.Text = PPCharts.Chart.ChartTitle.Text
            PPSlide.Shapes(3).Delete
            
            ' Paste chart: choose if linked to Excel or not '
            PPSlide.Shapes.PasteSpecial(DataType:=ppPasteShape, link:=msoFalse).Select
            
            ' reposition chart and textbox '
            PPSlide.Shapes(2).Width = 300
            With PPSlide.Shapes(3)
                .Top = PPSlide.Shapes(2).Top
                .Left = PPSlide.Shapes(2).Left + PPSlide.Shapes(2).Width + 50
                .Width = 475
                .Height = PPSlide.Shapes(2).Height
            End With
            
        Next PPCharts
nextSheet:
    Next ws
    
End Sub
